buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		// Dependencies for gradle itself - probably don't want to modify this
		classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
	}
}

allprojects {
	description = 'Platooning code for Group Project team Delta'
	version = '1.0'

	repositories {
		jcenter()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'checkstyle'

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	compileJava {
		// Force compilers to use UTF-8
		options.encoding =  'UTF-8'
		// Compile using jdk 7 libraries to suppress warnings about bootstrap file
		options.bootstrapClasspath = files(project.rootProject.file("lib/jre7-rt.jar"))
	}

	dependencies  {
		// Dependencies for running the code. testCompile means dependencies used for tests, while compile means
		// dependencies used for production code
		testCompile 'junit:junit:4.12'
		testCompile "org.mockito:mockito-core:2.+"
	}

	checkstyle {
		configFile = rootProject.file('config/checkstyle/checkstyle.xml')
		configDir = rootProject.file('config/checkstyle')
	}

	test {
		testLogging {
			events "passed", "skipped", "failed", "standardOut", "standardError"
		}
	}
}
